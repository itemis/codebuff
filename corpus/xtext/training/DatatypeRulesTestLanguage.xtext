/*******************************************************************************
 * Copyright (c) 2008 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 *******************************************************************************/
grammar org.eclipse.xtext.parser.datatyperules.DatatypeRulesTestLanguage with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate datatypeRulesTestLanguage "http://www.eclipse.org/2008/tmf/xtext/DatatypeRulesTest"
 
CompositeModel:
  (model+=Model)+; 

Model: 
  id=NestedModelId (':' value=Fraction)? ('#' vector=Vector)? ('+' dots=Dots)? ('*' double=Double)? ';';
  
ModelId returns ecore::EString:
  ID '.' ID;

NestedModelId :
  ModelId '.' ModelId;
  
Fraction returns ecore::EBigDecimal:
  INT ('/' INT)?;
  
Vector :
  '(' INT INT ')';
  
Dots returns ecore::EString:
  ( '.' '.' ) | '..';

Double returns ecore::EDouble:
	'-'? INT? '.' INT ('E' '-'? INT)?;